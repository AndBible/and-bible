apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'

def jsDir = "bibleview-js"

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def getGitDescribe = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--always'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

task npmUpgrade(type: Exec) {
    inputs.file "$jsDir/package.json"
    outputs.file "$jsDir/node_modules/.bin/npm"
    workingDir jsDir
    // Workaround for F-droid, which has buggy npm version 5.8, that always fails when installing packages.
    if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
        commandLine "npx.cmd", "npm@latest", "install", "--save-dev", "npm@latest"
    }
    else {
        commandLine "npx", "npm@latest", "install", "--save-dev", "npm@latest"
    }
}

task npmInstall(type: Exec, dependsOn: npmUpgrade) {
    inputs.file "$jsDir/package.json"
    outputs.dir "$jsDir/node_modules"

    workingDir jsDir
    if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
        commandLine "$rootDir/app/$jsDir/node_modules/.bin/npm.cmd", "install"
    }
    else {
        commandLine "node_modules/.bin/npm", "install"
    }
}

task vuecli(type: Exec, dependsOn: npmInstall) {
    inputs.file "$jsDir/package.json"
    inputs.file "$jsDir/vue.config.js"
    inputs.file "$jsDir/babel.config.js"
    inputs.dir "$jsDir/src"
    inputs.dir "$jsDir/public"
    outputs.dir "$jsDir/dist"
    println("Task names "+gradle.startParameter.taskNames)
    def taskNames = gradle.startParameter.taskNames
    def isDebug = taskNames.contains(":app:assembleDebug") || taskNames.contains(":app:packageDebug")
    def buildCmd

    if(!isDebug) {
        println("Building for production")
        buildCmd = "build-production"
    } else {
        println("Building for debug")
        buildCmd = "build-debug"
    }
    workingDir jsDir
    if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
        commandLine "$rootDir/app/$jsDir/node_modules/.bin/npm.cmd", "run", buildCmd
    }
    else {
        commandLine "node_modules/.bin/npm", "run", buildCmd
    }
}

task buildLoaderJs(type: Sync, dependsOn: vuecli) {
    from("$jsDir/dist")
    into("src/main/assets/bibleview-js")
}

task jsTests(type: Exec, dependsOn: npmInstall) {
    workingDir jsDir
    commandLine "node_modules/.bin/npm", "run", "test:unit"
}

preBuild.dependsOn buildLoaderJs
check.dependsOn jsTests

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    dexOptions {
        javaMaxHeapSize "4g"
    }

    /** these config values override those in AndroidManifest.xml.  Can also set versionCode and versionName */
    defaultConfig {
        applicationId "net.bible.android.activity"
        minSdkVersion 21
        targetSdkVersion 29
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        buildConfigField "String", "GitHash", "\"${getGitHash()}\""
        buildConfigField "String", "GitDescribe", "\"${getGitDescribe()}\""
        buildConfigField "String", "BuildDate", "\"${new Date().format('dd/MM/YY HH:mm:ss')}\""
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        missingDimensionStrategy 'react-native-camera', 'general'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
        }
        debug {
            def propsFile = rootProject.file('local.properties')
            if (propsFile.exists()) {
                def props = new Properties()
                props.load(new FileInputStream(propsFile))
                println("App suffix: " + props['APP_SUFFIX'])

                if (props['APP_SUFFIX'] != null) {
                    applicationIdSuffix props['APP_SUFFIX']
                }
            }
//			minifyEnabled true
//			useProguard true
//			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
//			zipAlignEnabled true
        }
    }

    lintOptions {
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
        warning 'InvalidPackage' 
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        // prevent logger errors
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
            all {
                testLogging {
                    events "passed", "skipped", "failed"
                    exceptionFormat "full"
                }
            }
        }

    }

    bundle {
        language {
            enableSplit = false
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/DEPENDENCIES'
    }

}

dependencies {
    implementation project(':db')
    // Appcompat:
    // 1.2.0+ releases (until 1.3.0-alpha02 at least) have issue with translations
    // not showing up on MainBibleActivity. Thus reverting to 1.0.2 for now.
    // https://issuetracker.google.com/issues/141132133
    implementation 'androidx.appcompat:appcompat:1.1.0'

    implementation 'androidx.drawerlayout:drawerlayout:1.1.1'
    implementation 'androidx.media:media:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.webkit:webkit:1.4.0'

    //implementation 'androidx.recyclerview:recyclerview-selection:1.0.0'

    //implementation 'com.jaredrummler:colorpicker:1.1.0'
    implementation 'com.github.AndBible:ColorPicker:ab-fix-1'

    implementation 'com.google.android.material:material:1.2.1'

    // allow annotations like UIThread, StringRes see: https://developer.android.com/reference/android/support/annotation/package-summary.html
    implementation 'androidx.annotation:annotation:1.1.0'

    implementation "androidx.room:room-runtime:$room_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlinx_serialization_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2'

    implementation 'com.google.dagger:dagger:2.27'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.2.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.2.2'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.27'
    kapt 'com.google.dagger:dagger-compiler:2.27'

    implementation 'de.greenrobot:eventbus:2.4.1'

    // 3.9 contains java8 bytecode, thus staying in 3.8
    implementation 'org.apache.commons:commons-lang3:3.8.1'
    implementation 'org.apache.commons:commons-text:1.9'

    implementation "com.github.AndBible:jsword:$jsword_version"

    // make sure this is same version as in jsword!
    implementation 'org.jdom:jdom2:2.0.6'

    debugImplementation 'com.facebook.stetho:stetho:1.5.1'

    testImplementation project(':db')

    // TESTS
    testImplementation "com.github.AndBible:robolectric:4.3.1-andbible3"
    //testImplementation "org.robolectric:shadows-multidex:4.3.1"
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    testImplementation 'org.hamcrest:hamcrest-library:2.2'
    testImplementation 'org.mockito:mockito-core:3.3.3'
    testImplementation 'junit:junit:4.13'

    // Android UI testing

    // Core library
    androidTestImplementation 'androidx.test:core:1.2.0'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'

    // Assertions
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.ext:truth:1.2.0'
    androidTestImplementation 'com.google.truth:truth:0.42'

    // Espresso dependencies
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-accessibility:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.2.0'
    androidTestImplementation 'androidx.test.espresso.idling:idling-concurrent:3.2.0'

    // The following Espresso dependency can be either "implementation"
    // or "androidTestImplementation", depending on whether you want the
    // dependency to appear on your APK's compile classpath or the test APK
    // classpath.
    androidTestImplementation 'androidx.test.espresso:espresso-idling-resource:3.2.0'
}

repositories {
	mavenCentral()
}
